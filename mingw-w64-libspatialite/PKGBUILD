# Maintainer: Alexey Kasatkin <alexeikasatkin@gmail.com>
# ArchLinux maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Brian Galey <bkgaley at gmail dot com>
# Contributor: Pietro Zambelli <peter.zamb at gmail dot com>

_realname=libspatialite
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.0.0
pkgrel=1
pkgdesc="SQLite extension to support spatial data types and operations (mingw-w64)"
arch=('any')
url="https://www.gaia-gis.it/fossil/libspatialite/index"
license=('MPL' 'GPL' 'LGPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config" "automake" "autoconf")
depends=("${MINGW_PACKAGE_PREFIX}-geos"
         "${MINGW_PACKAGE_PREFIX}-libfreexl"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-proj"
         "${MINGW_PACKAGE_PREFIX}-sqlite3"
         "${MINGW_PACKAGE_PREFIX}-libiconv")
options=('strip')
source=("https://www.gaia-gis.it/gaia-sins/${_realname}-${pkgver}.tar.gz")
sha256sums=('7b7fd70243f5a0b175696d87c46dde0ace030eacc27f39241c24bac5dfac6dac')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  sed -i configure.ac -e "s|mingw32|${MINGW_CHOST}|g"

  # Jeroen: workarounds for static proj/geos
  PROJLIBS=$(pkg-config --libs-only-l --static proj)
  GEOSLIBS="-lgeos_c -lgeos -lstdc++ -lm"
  XML2LIBS="$(pkg-config --libs-only-l --static libxml-2.0)"
  FREEXLLIBS="$(pkg-config --libs-only-l --static freexl)"
  sed -i 's/--ldflags`/--static-clibs`" -lstdc++"/g' configure.ac
  sed -i "s/-lgeos_c/${GEOSLIBS}/g" configure.ac
  sed -i "s/-lspatialite/-lspatialite ${PROJLIBS} ${GEOSLIBS} ${FREEXLLIBS} ${XML2LIBS}/" spatialite.pc.in
  autoreconf
}

build() {
  [[ -d "${srcdir}"/build-${MINGW_CHOST} ]] && rm -rf "${srcdir}"/build-${MINGW_CHOST}
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"
  
  # Remove CPPFLAGS for spatialite 5.0 (which should support proj6)
  LIBXML2_LIBS="${XML2LIBS}" \
  LIBS="${PROJLIBS}" \
  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-libxml2 \
    --disable-minizip \
    --with-geosconfig="${MINGW_PREFIX}/bin/${MINGW_CHOST}-geos-config" \
    --enable-static \
    --disable-shared

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install
}
