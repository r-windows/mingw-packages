# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=pango
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.42.4
pkgrel=1
pkgdesc="A library for layout and rendering of text (mingw-w64)"
arch=('any')
url="http://www.pango.org"

# TODO: what is the license for the package as a whole? The README file says
# that most of the code is LGPL but some parts are "GPL/FreeType". However this
# non-LGPL note makes reference to non-existing files, so would LGPL apply to
# the whole code? If this note is still correct, are the GPL/FreeType parts
# under either or both of these licenses, and which versions? What license is
# compatible with the answers?
license=(partial:'LGPL2')

makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-pkg-config"
            "gtk-doc")
depends=("${MINGW_PACKAGE_PREFIX}-cairo"
         "${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-glib2"
#         "${MINGW_PACKAGE_PREFIX}-harfbuzz"
         "${MINGW_PACKAGE_PREFIX}-fribidi")
options=('staticlibs' 'strip' 'emptydirs')
source=("https://download.gnome.org/sources/pango/${pkgver:0:4}/${_realname}-${pkgver}.tar.xz"
        0001-no-unconditional-xft-please.all.patch
        0001-build-move-usp10-before-gdi32.patch)
sha256sums=('1d2b74cd63e8bd41961f2f8d952355aa0f9be6002b52c8aa7699d9f5da597c9d'
            'bebab6128258d300e677df0751177f5c30235d0a49c150d97987d0f00b309f35'
            'f07bfd26bc4eabb03ea0294d2d428e617a6fb505afec0d1c40dc8af659d22a97')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/0001-no-unconditional-xft-please.all.patch
  #patch -p1 -i ${srcdir}/0001-build-move-usp10-before-gdi32.patch
  sed -i.bak '/GOBJECT_INTROSPECTION_CHECK/d' configure.ac
  #autoreconf -fi
  sed -i.bak 's/HAVE_INTROSPECTION/HAVE_CORE_TEXT/g' pango/Makefile.am
  sed -i.bak 's/HAVE_INTROSPECTION/HAVE_CORE_TEXT/g' win32/Makefile.am
  sed -i.bak 's/tools tests win32/tools win32/' Makefile.am
  NOCONFIGURE=1 ./autogen.sh  
  sed -i 's/have_libthai=true/have_libthai=false/' configure
  #sed -i.bak '/GOBJECT_INTROSPECTION_CHECK/d' configure
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  # There are no configure options for --without-harfbuzz and --without-introspection
  # but if these aren't installed, pango builds without it.
  # Harfbuzz integration currently fails because it was built without gobject bindings.
  #export LIBS="$(pkgconfig --libs-only-l --static cairo glib-2.0)"
  export LIBS="-lgdiplus -lgdi32"
  export PKG_CONFIG="${MINGW_PREFIX}/bin/pkg-config --static"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-gtk-doc-html=no \
    --disable-debug \
    --disable-shared \
    --enable-static

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install

  # License
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/README"  "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/README"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
