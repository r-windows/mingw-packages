# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libwebp
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.1.0
pkgrel=1
pkgdesc="the WebP library. Includes conversion tools (mingw-w64)"
arch=('any')
url="https://developers.google.com/speed/webp/"
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-giflib"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-libtiff")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
source=(https://storage.googleapis.com/downloads.webmproject.org/releases/webp/${_realname}-${pkgver}.tar.gz)
sha256sums=('98a052268cc4d5ece27f76572a7f50293f439c17a98e67c4ea0c7ed6f50ef043')
validpgpkeys=('6B0E6B70976DE303EDF2F601F9C3D6BDB8232B5D')

build() {
  mkdir -p ${srcdir}/${MINGW_CHOST}
  cd "${srcdir}/${MINGW_CHOST}"
  export LIBS="-ltiff -ljpeg -lz"
  ../${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-static \
    --disable-shared \
    --enable-libwebpmux \
    --enable-libwebpdemux \
    --enable-libwebpdecoder \
    --enable-libwebpextras
  make -j1
}

package() {
  cd "${srcdir}/$MINGW_CHOST"
  make install DESTDIR="${pkgdir}"
}
