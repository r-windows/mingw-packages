# Maintainer: Oleg Tolmatcev <oleg.tolmatcev@gmail.com>

_realname=qpdf
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=10.3.1
pkgrel=9100
pkgdesc="QPDF is a program that does structural, content-preserving transformations on PDF files (mingw-w64)"
arch=('any')
url="http://qpdf.sourceforge.net/"
license=("custom:Artistic-2.0")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-libjpeg"
             "${MINGW_PACKAGE_PREFIX}-pcre"
             "${MINGW_PACKAGE_PREFIX}-zlib")
source=(https://github.com/qpdf/qpdf/releases/download/release-${_realname}-${pkgver}/${_realname}-${pkgver}.tar.gz
        "0002-enable-advapi32-mingw.patch"
        "0003-install-rule.patch")
sha256sums=('d3e6b862098c6357d04390fd30d08c94aa2cf7a3bb2dcabd3846df5eb57367d6'
            'SKIP'
            'SKIP')
validpgpkeys=('C2C96B10011FE009E6D1DF828A75D10998012C7E') # Jay Berkenbilt <ejb@ql.org>

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i "${srcdir}/0002-enable-advapi32-mingw.patch"
  patch -p1 -i "${srcdir}/0003-install-rule.patch"

  ./autogen.sh
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  cp -rf ${_realname}-${pkgver} build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}

  ./configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --disable-crypto-gnutls \
      --disable-crypto-openssl \
      --disable-test-compare-images \
      --enable-external-libs \
      --with-buildrules=mingw
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR=${pkgdir} install

  # libqpdf.a is actually an import library so unusable
  rm -R ${pkgdir}${MINGW_PREFIX}/{lib,include}
  rm ${pkgdir}${MINGW_PREFIX}/bin/{fix-qdf,zlib-flate}.exe
}
