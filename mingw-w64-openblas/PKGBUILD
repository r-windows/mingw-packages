# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: JÃ¼rgen Pfeifer <juergen@familiepfeifer.de>

_realname=OpenBLAS
pkgbase=mingw-w64-openblas
pkgname="${MINGW_PACKAGE_PREFIX}-openblas"
pkgver=0.3.5
pkgrel=1
pkgdesc="An optimized BLAS library based on GotoBLAS2 1.13 BSD, providing optimized blas, lapack, and cblas (mingw-w64)"
arch=('any')
url="http://www.openblas.net/"
license=('BSD')
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-openblas-git")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}"
          "${MINGW_PACKAGE_PREFIX}-${_realname}-git")
depends=("${MINGW_PACKAGE_PREFIX}-libwinpthread")
makedepends=("perl"
             "${MINGW_PACKAGE_PREFIX}-gcc-fortran")
options=('!buildflags')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/xianyi/OpenBLAS/archive/v${pkgver}.tar.gz
        001-defaultlib-not-for-gcc.patch
        002-lgfortran-requires-lquadmath.patch)
install=${_realname}.install
sha256sums=('0950c14bd77c90a6427e26210d6dab422271bc86f9fc69126725833ecdaa0e85'
            'd9192818d0b0a9e7cbed7cc2572dfd380aa311be5ce4d012c0cb55211013db37'
            'ab1c10a66b4d0332f2339f1169c0e3ebd48d9b8bf7afaec3f6a0d62099b52941')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-defaultlib-not-for-gcc.patch
  patch -p1 -i ${srcdir}/002-lgfortran-requires-lquadmath.patch
}

build() {
  cp -rf "${srcdir}"/${_realname}-${pkgver} "${srcdir}"/build-${CARCH}
  cd "${srcdir}"/build-${CARCH}

  unset CFLAGS
  unset CXXFLAGS

  make LIBNAME=libopenblasp.a NO_LAPACK=0 NO_AVX512=1 DYNAMIC_ARCH=1   \
       CC=${MINGW_PREFIX}/bin/gcc                                      \
       FC=${MINGW_PREFIX}/bin/gfortran                                 \
       OPENBLAS_INCLUDE_DIR=${MINGW_PREFIX}/include/${_realname}       \
       USE_THREAD=1 NUM_THREADS=64 \
	   NO_SHARED=1
}

package() {
  cd "${srcdir}"/build-${CARCH}

  mkdir -p "${pkgdir}${MINGW_PREFIX}/bin"
  mkdir -p "${pkgdir}${MINGW_PREFIX}/lib"

  make LIBNAME=libopenblasp.a \
       DESTDIR="${pkgdir}" PREFIX="${MINGW_PREFIX}"  \
       OPENBLAS_INCLUDE_DIR=${MINGW_PREFIX}/include/${_realname} \
       install

  if [ -f libopenblasp.a ]; then
    rm -f "${pkgdir}$MINGW_PREFIX/lib/libopenblasp.a"
    rm -f "${pkgdir}$MINGW_PREFIX/lib/libopenblas.a"
    cp -f libopenblasp.a "${pkgdir}${MINGW_PREFIX}/lib/libopenblas.a"
  else
    echo "Static library not found" >&2
    exit 1
  fi
}
